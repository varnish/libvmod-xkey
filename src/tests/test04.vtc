varnishtest "Test xkey vmod multiple objects multiple keys cleanup"

# Testcase lays the ground work for the cleanup code to run, though it
# has no way of learning the result :( Any asserts triggered should
# show though.

server s1 {
	rxreq
	txresp -hdr "xkey: asdf" -hdr "xkey: fdsa"
	rxreq
	txresp -hdr "xkey: asdf" -hdr "xkey: fdsa"
} -start

varnish v1 -vcl+backend {
	import xkey from "${vmod_topbuild}/src/.libs/libvmod_xkey.so";

	sub vcl_recv {
		if (req.http.xkey-purge) {
			if (xkey.purge(req.http.xkey-purge) != 0) {
				return (synth(200, "Purged"));
			} else {
				return (synth(404, "No key"));
			}
		}
	}

	sub vcl_backend_response {
		set beresp.ttl = 60s;
		set beresp.grace = 0s;
		set beresp.keep = 0s;
	}
} -start

client c1 {
	txreq
	rxresp
	txreq -url /2
	rxresp
} -run

varnish v1 -expect n_object == 2

varnish v1 -vcl+backend {
}

varnish v1 -cliok "vcl.discard vcl1"

delay 2

client c1 {
	txreq
	rxresp
	expect resp.status == 200
} -run

varnish v1 -expect n_object == 2
